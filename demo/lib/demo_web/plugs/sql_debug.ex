defmodule DemoWeb.Plugs.SqlDebug do
  @moduledoc """
  Demo-only plug that captures the SQL generated by Ecto via telemetry
  and returns it as an `x-debug-sql` response header.
  """
  import Plug.Conn

  def init(opts), do: opts

  def call(conn, _opts) do
    Process.put(:pgrest_sql_queries, [])

    handler_id = "sql-debug-#{:erlang.unique_integer()}"

    :telemetry.attach(
      handler_id,
      [:demo, :repo, :query],
      &__MODULE__.handle_query_event/4,
      nil
    )

    register_before_send(conn, fn conn ->
      queries =
        Process.get(:pgrest_sql_queries, [])
        |> Enum.reverse()
        |> Enum.reject(&String.starts_with?(&1, "SELECT count(*)"))

      :telemetry.detach(handler_id)

      annotated = annotate_queries(queries)
      all_sql = Enum.join(annotated, "\n---\n")
      put_resp_header(conn, "x-debug-sql", Base.encode64(all_sql))
    end)
  end

  @doc false
  def handle_query_event(_event, _measurements, metadata, _config) do
    Process.put(:pgrest_sql_queries, [metadata.query | Process.get(:pgrest_sql_queries, [])])
  end

  defp annotate_queries([_single] = queries), do: queries

  defp annotate_queries(queries) when length(queries) > 1 do
    Enum.map(queries, fn query ->
      table = extract_table(query)

      if String.contains?(query, "ANY") do
        "-- Preload associated #{table}\n" <> query
      else
        "-- Fetch #{table}\n" <> query
      end
    end)
  end

  defp annotate_queries(queries), do: queries

  defp extract_table(sql) do
    case Regex.run(~r/FROM\s+"(\w+)"/, sql) do
      [_, table] -> table
      _ -> "records"
    end
  end
end
